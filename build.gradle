version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.51'
    ext.botlin_version = '0.2.1'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'

repositories {
    mavenCentral()
    maven { url 'https://dl.bintray.com/mizoguche/maven/' }

    // For simple-slack-api
    // if you don't need SlackEngine, remove this line
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "info.mizoguche:botlin:$botlin_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task run(type: JavaExec) {
    classpath = files("build/libs/${rootProject.name}-${version}.jar")
    classpath += sourceSets.main.runtimeClasspath
    main = 'botlin.MainKt'
    dependsOn assemble
}

task stage(type: Jar, dependsOn: [clean, jar]) {
    archiveName = "${rootProject.name}.jar"
    manifest {
        attributes 'Main-Class': 'botlin.MainKt'
    }

    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    from "$buildDir/classes/main"
}
